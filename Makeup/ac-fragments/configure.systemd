dnl Makeup configure boilerplate.
dnl
dnl Copyright 2018, Ron <ron@debian.org>
dnl
dnl This file is distributed under the terms of the GNU GPL version 2.
dnl
dnl As a special exception to the GPL, it may be distributed without
dnl modification as a part of a program using a makeup generated build
dnl system, under the same distribution terms as the program itself.

dnl There are platforms where we know systemd won't be present, so don't annoy
dnl their users with its cruft unless we're explicitly passed --enable-systemd.
mu_expect_systemd=no

case $host in
    *-*-linux* )
	mu_expect_systemd=yes
    ;;
esac

dnl Option and substvar for installing systemd units.
AC_ARG_ENABLE([systemd],
              [AS_HELP_STRING([--enable-systemd],
                              [install systemd unit files (default yes on Linux, else no)])],
              [mu_cv_enable_systemd=$enableval],
              [mu_cv_enable_systemd=$mu_expect_systemd])

dnl Don't bother setting SYSTEMD_UNIT_DIR if we're using --disable-systemd.
dnl It being empty provides a signal to not try and install any units in that
dnl case too.
dnl
dnl There's some additional hackery we need to do here, because systemd has a
dnl deep assumption of everything being installed into vendor space and the
dnl systemd.pc it creates doesn't doesn't respect an alternative $prefix, it
dnl just hardcodes the one which systemd itself was built with.
dnl
dnl So right now, the least horrid thing we can do is, if we are building with
dnl --prefix=/usr, then we install units to the vendor unit dir reported by
dnl systemd.pc, otherwise we install them to $libdir/systemd/system.  If users
dnl need or want something different to that then they can explicitly override
dnl the SYSTEMD_UNIT_DIR variable. This should work ok for --prefix=/usr/local,
dnl because systemd will search there too, but it won't work by default for any
dnl other oddball values of $prefix.  We could default to systemdsystemconfdir
dnl (aka /etc/systemd/system) for the oddball case, but if people are using an
dnl odd prefix, then they probably have their own plan for mapping that to
dnl locations which will actually be searched, so for now at least, we will
dnl respect that too.
dnl
dnl The fallback for ACM_PKG_CONFIG_GET_VAR here should probably instead be
dnl $EXP_LIBDIR/systemd/system too, but on systems with "split usr" the vendor
dnl units are installed in /lib, and on those with "merged usr" both /lib and
dnl /usr/lib point to the same place anyway, so it's probably the safest path
dnl to default to here for now if pkg-config support is not available.
AS_IF([test "$mu_cv_enable_systemd" = yes],[
      ACM_IF_VENDOR_BUILD([ACM_PKG_CONFIG_GET_VAR([SYSTEMD_UNIT_DIR],[systemd],
                                                  [systemdsystemunitdir],[/lib/systemd/system])],
                          [AC_MSG_CHECKING([for SYSTEMD_UNIT_DIR])
                           SYSTEMD_UNIT_DIR="$EXP_LIBDIR/systemd/system"
                           AC_MSG_RESULT([$SYSTEMD_UNIT_DIR (for prefix=$EXP_PREFIX)])
                          ])
    ])

AC_ARG_VAR([SYSTEMD_UNIT_DIR], [where to install systemd units])

AC_DEFINE_UNQUOTED([EM_USE_NOTIFY_SOCKET],
                   [$(test "$mu_cv_enable_systemd" != yes)$?],
                   [Build with service manager NOTIFY_SOCKET support])

